import Mathlib.Dynamics.BirkhoffSum.Average
import Mathlib.Dynamics.Ergodic.MeasurePreserving
import Mathlib.MeasureTheory.Function.L1Space
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Basic
import Mathlib.Tactic
import BirkhoffErgodicThm.PartialSupsPR

set_option maxHeartbeats 0

section BirkhoffMax

variable {Î± : Type*}

/- note that we must compose with .succ, as we want to allow `birkhoffMax`
   to be negative but `birkhoffSum f Ï† 0 = 0`.
-/
def birkhoffMax (f : Î± â†’ Î±) (Ï† : Î± â†’ â„) : â„• â†’o (Î± â†’ â„) :=
  partialSups (birkhoffSum f Ï† âˆ˜ .succ)

lemma birkhoffMax_succ : birkhoffMax f Ï† n.succ x = Ï† x + 0 âŠ” birkhoffMax f Ï† n (f x) := by
  have : birkhoffSum f Ï† âˆ˜ .succ = Î» k â†¦ Ï† + birkhoffSum f Ï† k âˆ˜ f
  Â· funext k x; dsimp
    rw [Nat.succ_eq_one_add, birkhoffSum_add f Ï† 1, birkhoffSum_one]; rfl
  nth_rw 1 [birkhoffMax, this, add_partialSups]; simp [-partialSups_succ]
  rw [partialSups_succ']; simp
  simp_rw [partialSups_apply, Function.comp_apply, â†partialSups_apply]; rfl

abbrev birkhoffMaxDiff (f : Î± â†’ Î±) (Ï† : Î± â†’ â„) (n : â„•) (x : Î±) :=
  birkhoffMax f Ï† (n + 1) x - birkhoffMax f Ï† n (f x)

theorem birkhoffMaxDiff_aux : birkhoffMaxDiff f Ï† n x = Ï† x - (0 âŠ“ birkhoffMax f Ï† n (f x)) := by
  rw [sub_eq_sub_iff_add_eq_add, birkhoffMax_succ, add_assoc, add_right_inj]
  change max _ _ + min _ _ = _
  rw [max_add_min, zero_add]

lemma birkhoffMaxDiff_antitone : Antitone (birkhoffMaxDiff f Ï†) := by
  intro m n h x
  rw [birkhoffMaxDiff_aux, birkhoffMaxDiff_aux]
  apply add_le_add_left
  simp
  right
  exact (birkhoffMax f Ï†).monotone' h _

@[measurability]
lemma birkhoffSum_measurable [MeasurableSpace Î±]
    {f : Î± â†’ Î±} (hf : Measurable f)
    {Ï† : Î± â†’ â„} (hÏ† : Measurable Ï†) :
    Measurable (birkhoffSum f Ï† n) := by
  apply Finset.measurable_sum
  measurability

@[measurability]
lemma birkhoffMax_measurable [MeasurableSpace Î±]
    {f : Î± â†’ Î±} (hf : Measurable f)
    {Ï† : Î± â†’ â„} (hÏ† : Measurable Ï†) :
    Measurable (birkhoffMax f Ï† n) := by
  induction n <;> unfold birkhoffMax <;> measurability

end BirkhoffMax


noncomputable section BirkhoffThm

open MeasureTheory Filter Topology

variable {Î± : Type*} [msÎ± : MeasurableSpace Î±] (Î¼ : Measure Î± := by volume_tac)
        [hÎ¼ : IsProbabilityMeasure Î¼]

def birkhoffSup (f : Î± â†’ Î±) (Ï† : Î± â†’ â„) (x : Î±) : EReal :=
  iSup Î» n â†¦ â†‘(birkhoffSum f Ï† (n + 1) x)

lemma birkhoffSup_measurable
    {f : Î± â†’ Î±} (hf : Measurable f)
    {Ï† : Î± â†’ â„} (hÏ† : Measurable Ï†) :
    Measurable (birkhoffSup f Ï†) := by
  unfold birkhoffSup
  measurability

def divergentSet (f : Î± â†’ Î±) (Ï† : Î± â†’ â„) : Set Î± := (birkhoffSup f Ï†)â»Â¹' {âŠ¤}

def invSigmaAlg (f : Î± â†’ Î±) : MeasurableSpace Î± where
  MeasurableSet' s := msÎ±.MeasurableSet' s âˆ§ fâ»Â¹' s = s
  measurableSet_empty := by
    constructor
    Â· exact msÎ±.measurableSet_empty
    Â· rfl
  measurableSet_compl s hs := by
    constructor
    Â· exact msÎ±.measurableSet_compl s hs.1
    Â· simp; exact hs.right
  measurableSet_iUnion s hs := by
    constructor
    Â· exact msÎ±.measurableSet_iUnion s (Î» i â†¦ (hs i).left)
    Â· simp; exact Set.iUnion_congr (Î» i â†¦ (hs i).right)

lemma divergentSet_invariant : f x âˆˆ divergentSet f Ï† â†” x âˆˆ divergentSet f Ï† := by
  constructor
  all_goals
    intro hx
    simp [divergentSet, birkhoffSup, iSup_eq_top] at *
    intro M hM
    cases' M using EReal.rec with a
    Â· use 0; apply EReal.bot_lt_coe
    case h_top => contradiction
  case mp =>
    specialize hx â†‘(- Ï† x + a) (EReal.coe_lt_top _)
    cases' hx with N hN
    simp_rw [EReal.coe_lt_coe_iff] at *
    rw [neg_add_lt_iff_lt_add, â†birkhoffSum_succ'] at hN
    use N + 1
  case mpr =>
    cases' hx â†‘(Ï† x + a) (EReal.coe_lt_top _) with N hN
    simp_rw [EReal.coe_lt_coe_iff] at *
    conv =>
      congr
      intro i
      rw [â†add_lt_add_iff_left (Ï† x), â†birkhoffSum_succ']
    cases' N with N
    Â· /- ugly case! :( -/
      cases' hx â†‘(birkhoffSum f Ï† 1 x) (EReal.coe_lt_top _) with N hNN
      cases' N with N
      Â· exfalso
        exact (lt_self_iff_false _).mp hNN
      Â· use N
        rw [EReal.coe_lt_coe_iff] at hNN
        apply lt_trans hN hNN
    Â· use N

lemma divergentSet_measurable
    {f : Î± â†’ Î±} (hf : Measurable f)
    {Ï† : Î± â†’ â„} (hÏ† : Measurable Ï†) :
    MeasurableSet (divergentSet f Ï†) := by
  apply measurableSet_preimage (birkhoffSup_measurable hf hÏ†)
  apply measurableSet_singleton

lemma divergentSet_invariant'
    {f : Î± â†’ Î±} (hf : Measurable f)
    {Ï† : Î± â†’ â„} (hÏ† : Measurable Ï†) :
    MeasurableSet[invSigmaAlg f] (divergentSet f Ï†) :=
  /- should be `Set.ext divergentSet_invariant` but it is VERY slow -/
  âŸ¨divergentSet_measurable hf hÏ†, funext (Î» _ â†¦ propext divergentSet_invariant)âŸ©

lemma birkhoffMax_tendsto_top_mem_divergentSet (hx : x âˆˆ divergentSet f Ï†) :
    Tendsto (birkhoffMax f Ï† Â· x) atTop atTop := by
  apply tendsto_atTop_atTop.mpr
  intro b
  simp [divergentSet, birkhoffSup, iSup_eq_top] at hx
  cases' hx b (EReal.coe_lt_top _) with N hN
  simp [EReal.coe_lt_coe_iff] at hN
  use N
  intro n hn
  apply le_trans (le_of_lt hN)
  exact le_partialSups_of_le (birkhoffSum f Ï† âˆ˜ .succ) hn x

lemma birkhoffMaxDiff_tendsto_of_mem_divergentSet (hx : x âˆˆ divergentSet f Ï†) :
    Tendsto (birkhoffMaxDiff f Ï† Â· x) atTop (ğ“ (Ï† x)) := by
  have hx' : f x âˆˆ divergentSet f Ï† := divergentSet_invariant.mpr hx
  simp_rw [birkhoffMaxDiff_aux]
  nth_rw 2 [â†sub_zero (Ï† x)]
  apply Tendsto.sub tendsto_const_nhds
  have := birkhoffMax_tendsto_top_mem_divergentSet hx'
  cases' tendsto_atTop_atTop.mp this 0 with N hN
  apply tendsto_atTop_of_eventually_const (iâ‚€ := N)
  intro i hi
  exact inf_of_le_left (hN i hi)

lemma limsup_birkhoffAverage_nonpos_of_not_mem_divergentSet
    (hx : x âˆ‰ divergentSet f Ï†) :
    limsup (Î» n â†¦ (birkhoffAverage â„ f Ï† n x).toEReal) atTop â‰¤ 0 := by
  /- it suffices to show there are upper bounds â‰¤ Îµ for all Îµ > 0 -/
  apply le_of_forall_le_of_dense
  intro Îµ' hÎµ

  /- it suffices show for Îµ â‰  âŠ¥ -/
  cases' Îµ' using EReal.rec with Îµ
  case h_bot => contradiction
  case h_top => exact le_top
  conv at hÎµ => lhs; change â†‘(0 : â„)
  rw [EReal.coe_lt_coe_iff] at hÎµ

  /- from `hx` hypothesis, the birkhoff sums are bounded above -/
  simp [divergentSet, birkhoffSup, iSup_eq_top] at hx
  rcases hx with âŸ¨M', M_lt_top, M_is_boundâŸ©

  /- the upper bound is, in fact, a real number -/
  cases' M' using EReal.rec with M
  case h_bot => exfalso; exact (EReal.bot_lt_coe _).not_le (M_is_bound 0)
  case h_top => contradiction
  simp_rw [EReal.coe_le_coe_iff] at M_is_bound

  /- use archimedian property of reals -/
  cases' Archimedean.arch M hÎµ with N hN
  have upperBound (n : â„•) (hn : N â‰¤ n) : birkhoffAverage â„ f Ï† (n + 1) x < Îµ
  Â· have : M < (n + 1) â€¢ Îµ
    Â· exact hN.trans_lt $ smul_lt_smul_of_pos_right (Nat.lt_succ_of_le hn) hÎµ
    rw [nsmul_eq_smul_cast â„] at this
    apply (inv_smul_lt_iff_of_pos (Nat.cast_pos.mpr (Nat.zero_lt_succ n))).mpr
    exact (M_is_bound n).trans_lt this

  apply sInf_le; simp
  use N + 1
  intro n hn
  specialize upperBound n.pred (Nat.le_pred_of_lt hn)
  rw [â†Nat.succ_pred_eq_of_pos (Nat.zero_lt_of_lt hn)]
  apply le_of_lt upperBound


/- From now on, assume f is measure-preserving and Ï† is integrable. -/
variable {f : Î± â†’ Î±} (hf : MeasurePreserving f Î¼ Î¼)
         {Ï† : Î± â†’ â„} (hÏ† : Integrable Ï† Î¼) (hÏ†' : Measurable Ï†) /- seems necessary? -/

lemma iterates_integrable : Integrable (Ï† âˆ˜ f^[i]) Î¼ := by
  apply (integrable_map_measure _ _).mp
  Â· rwa [(hf.iterate i).map_eq]
  Â· rw [(hf.iterate i).map_eq]
    exact hÏ†.aestronglyMeasurable
  exact (hf.iterate i).measurable.aemeasurable

lemma birkhoffSum_integrable : Integrable (birkhoffSum f Ï† n) Î¼ := by
  unfold birkhoffSum
  apply integrable_finset_sum
  intros
  exact iterates_integrable Î¼ hf hÏ†

lemma birkhoffMax_integrable : Integrable (birkhoffMax f Ï† n) Î¼ := by
  unfold birkhoffMax
  induction' n with n hn
  Â· simpa
  Â· simp
    exact Integrable.sup hn (birkhoffSum_integrable Î¼ hf hÏ†)

lemma birkhoffMaxDiff_integrable : Integrable (birkhoffMaxDiff f Ï† n) Î¼ := by
  unfold birkhoffMaxDiff
  apply Integrable.sub
  Â· exact birkhoffMax_integrable Î¼ hf hÏ†
  Â· apply (integrable_map_measure _ _).mp
    Â· rw [hf.map_eq]
      exact (birkhoffMax_integrable Î¼ hf hÏ†)
    Â· rw [hf.map_eq]
      exact (birkhoffMax_integrable Î¼ hf hÏ†).aestronglyMeasurable
    exact hf.measurable.aemeasurable

lemma abs_le_bound {a b c : â„} : a â‰¤ b â†’ b â‰¤ c â†’ abs b â‰¤ max (abs a) (abs c) := by
  simp_rw [abs_eq_max_neg, max_le_iff]
  aesop

lemma int_birkhoffMaxDiff_in_divergentSet_tendsto :
    Tendsto (Î» n â†¦ âˆ« x in divergentSet f Ï†, birkhoffMaxDiff f Ï† n x âˆ‚Î¼) atTop
            (ğ“ $ âˆ« x in divergentSet f Ï†, Ï† x âˆ‚ Î¼) := by
  apply MeasureTheory.tendsto_integral_of_dominated_convergence (abs Ï† âŠ” abs (birkhoffMaxDiff f Ï† 0))
  Â· intro n
    exact (birkhoffMaxDiff_integrable Î¼ hf hÏ†).aestronglyMeasurable.restrict
  Â· apply Integrable.sup <;> apply Integrable.abs
    Â· exact hÏ†.restrict
    Â· exact (birkhoffMaxDiff_integrable Î¼ hf hÏ†).restrict
  Â· intro n
    apply ae_of_all
    intro x
    rw [Real.norm_eq_abs]
    apply abs_le_bound
    Â· rw [birkhoffMaxDiff_aux]; simp
    Â· apply birkhoffMaxDiff_antitone (zero_le n)
  Â· apply (ae_restrict_iff' _).mpr
    Â· apply ae_of_all
      intro x hx
      apply birkhoffMaxDiff_tendsto_of_mem_divergentSet hx
    Â· exact divergentSet_measurable hf.measurable hÏ†'

lemma int_birkhoffMaxDiff_in_divergentSet_nonneg :
    0 â‰¤ âˆ« x in divergentSet f Ï†, birkhoffMaxDiff f Ï† n x âˆ‚Î¼ := by
  unfold birkhoffMaxDiff
  have : (Î¼.restrict (divergentSet f Ï†)).map f = Î¼.restrict (divergentSet f Ï†)
  Â· nth_rw 1 [
      â†(divergentSet_invariant' hf.measurable hÏ†').2,
      â†Î¼.restrict_map hf.measurable (divergentSet_measurable hf.measurable hÏ†'),
      hf.map_eq
    ]
  have mi {n : â„•} := birkhoffMax_integrable Î¼ hf hÏ† (n := n)
  have mm {n : â„•} := birkhoffMax_measurable hf.measurable hÏ†' (n := n)
  rw [integral_sub, sub_nonneg]
  Â· rw [â†integral_map (hf.aemeasurable.restrict) mm.aestronglyMeasurable, this]
    apply integral_mono mi.restrict mi.restrict ((birkhoffMax f Ï†).monotone (Nat.le_succ _))
  Â· exact mi.restrict
  Â· apply (integrable_map_measure mm.aestronglyMeasurable hf.aemeasurable.restrict).mp
    rw [this]
    exact mi.restrict

lemma int_in_divergentSet_nonneg : 0 â‰¤ âˆ« x in divergentSet f Ï†, Ï† x âˆ‚Î¼ :=
  le_of_tendsto_of_tendsto' tendsto_const_nhds
    (int_birkhoffMaxDiff_in_divergentSet_tendsto Î¼ hf hÏ† hÏ†')
    (Î» _ â†¦ int_birkhoffMaxDiff_in_divergentSet_nonneg Î¼ hf hÏ† hÏ†')
