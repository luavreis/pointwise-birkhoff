import Mathlib.Dynamics.BirkhoffSum.Average
import Mathlib.Dynamics.Ergodic.MeasurePreserving
import Mathlib.MeasureTheory.Function.L1Space
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Basic
import Mathlib.Tactic
import BirkhoffErgodicThm.PartialSupsPR
import BirkhoffErgodicThm.BirkhoffSumPR
import BirkhoffErgodicThm.FilterPR

section BirkhoffMax

variable {Î± : Type*}

/- note that we must compose with .succ, as we want to allow `birkhoffMax`
   to be negative but `birkhoffSum f Ï† 0 = 0`.
-/
def birkhoffMax (f : Î± â†’ Î±) (Ï† : Î± â†’ â„) : â„• â†’o (Î± â†’ â„) :=
  partialSups (birkhoffSum f Ï† âˆ˜ .succ)

lemma birkhoffMax_succ : birkhoffMax f Ï† n.succ x = Ï† x + 0 âŠ” birkhoffMax f Ï† n (f x) := by
  have : birkhoffSum f Ï† âˆ˜ .succ = Î» k â†¦ Ï† + birkhoffSum f Ï† k âˆ˜ f
  Â· funext k x; dsimp
    rw [Nat.succ_eq_one_add, birkhoffSum_add f Ï† 1, birkhoffSum_one]; rfl
  nth_rw 1 [birkhoffMax, this, add_partialSups]; simp [-partialSups_succ]
  rw [partialSups_succ']; simp
  simp_rw [partialSups_apply, Function.comp_apply, â† partialSups_apply]; rfl

abbrev birkhoffMaxDiff (f : Î± â†’ Î±) (Ï† : Î± â†’ â„) (n : â„•) (x : Î±) :=
  birkhoffMax f Ï† (n + 1) x - birkhoffMax f Ï† n (f x)

theorem birkhoffMaxDiff_aux : birkhoffMaxDiff f Ï† n x = Ï† x - (0 âŠ“ birkhoffMax f Ï† n (f x)) := by
  rw [sub_eq_sub_iff_add_eq_add, birkhoffMax_succ, add_assoc, add_right_inj]
  change max _ _ + min _ _ = _
  rw [max_add_min, zero_add]

lemma birkhoffMaxDiff_antitone : Antitone (birkhoffMaxDiff f Ï†) := by
  intro m n h x
  rw [birkhoffMaxDiff_aux, birkhoffMaxDiff_aux]
  apply add_le_add_left
  simp
  right
  exact (birkhoffMax f Ï†).monotone' h _

@[measurability]
lemma birkhoffSum_measurable [MeasurableSpace Î±]
    {f : Î± â†’ Î±} (hf : Measurable f)
    {Ï† : Î± â†’ â„} (hÏ† : Measurable Ï†) :
    Measurable (birkhoffSum f Ï† n) := by
  apply Finset.measurable_sum
  measurability

@[measurability]
lemma birkhoffMax_measurable [MeasurableSpace Î±]
    {f : Î± â†’ Î±} (hf : Measurable f)
    {Ï† : Î± â†’ â„} (hÏ† : Measurable Ï†) :
    Measurable (birkhoffMax f Ï† n) := by
  induction n <;> unfold birkhoffMax <;> measurability

end BirkhoffMax

section InvariantSets

open MeasureTheory

def invariantSets [ms : MeasurableSpace Î±] (f : Î± â†’ Î±) : MeasurableSpace Î± where
  MeasurableSet' s := ms.MeasurableSet' s âˆ§ fâ»Â¹' s = s
  measurableSet_empty := âŸ¨ms.measurableSet_empty, rflâŸ©
  measurableSet_compl s hs := âŸ¨ms.measurableSet_compl s hs.1, (by simp [hs.right])âŸ©
  measurableSet_iUnion s hs := âŸ¨ms.measurableSet_iUnion s (Î» i â†¦ (hs i).left),
    by simp; exact Set.iUnion_congr (Î» i â†¦ (hs i).right)âŸ©

theorem invariantSets_le [ms : MeasurableSpace Î±] : invariantSets f â‰¤ ms := Î» _ hs => hs.1

theorem InvariantSets.invariant_of_measurable
    [MeasurableSpace Î±] [MeasurableSpace Î²] [MeasurableSingletonClass Î²]
    (f : Î± â†’ Î±) (Ï† : Î± â†’ Î²) (hÏ† : Measurable[invariantSets f] Ï†) :
    Ï† âˆ˜ f = Ï† := by
  funext x
  suffices x âˆˆ fâ»Â¹' (Ï†â»Â¹' {Ï† x}) by simpa
  rw [(hÏ† $ measurableSet_singleton (Ï† x)).2]
  rfl

end InvariantSets

noncomputable section BirkhoffThm

open MeasureTheory Filter Topology

variable {Î± : Type*} [msÎ± : MeasurableSpace Î±] (Î¼ : Measure Î± := by volume_tac)
        [hÎ¼ : IsProbabilityMeasure Î¼]

def birkhoffSup (f : Î± â†’ Î±) (Ï† : Î± â†’ â„) (x : Î±) : EReal := iSup Î» n â†¦ â†‘(birkhoffSum f Ï† (n + 1) x)

lemma birkhoffSup_measurable
    {f : Î± â†’ Î±} (hf : Measurable f)
    {Ï† : Î± â†’ â„} (hÏ† : Measurable Ï†) :
    Measurable (birkhoffSup f Ï†) := measurable_iSup
  (fun _ â†¦ Measurable.coe_real_ereal (birkhoffSum_measurable hf hÏ†))

def divergentSet (f : Î± â†’ Î±) (Ï† : Î± â†’ â„) : Set Î± := (birkhoffSup f Ï†)â»Â¹' {âŠ¤}

lemma divergentSet_invariant : f x âˆˆ divergentSet f Ï† â†” x âˆˆ divergentSet f Ï† := by
  constructor
  all_goals
    intro hx
    simp [divergentSet, birkhoffSup, iSup_eq_top] at *
    intro M hM
    cases' M using EReal.rec with a
    Â· use 0; apply EReal.bot_lt_coe
    case h_top => contradiction
  case mp =>
    cases' hx â†‘(- Ï† x + a) (EReal.coe_lt_top _) with N hN
    norm_cast at *
    rw [neg_add_lt_iff_lt_add, â† birkhoffSum_succ'] at hN
    use N + 1
  case mpr =>
    cases' hx â†‘(Ï† x + a) (EReal.coe_lt_top _) with N hN
    norm_cast at *
    conv =>
      congr
      intro i
      rw [â† add_lt_add_iff_left (Ï† x), â† birkhoffSum_succ']
    cases' N with N
    Â· /- ugly case! :( -/
      cases' hx â†‘(birkhoffSum f Ï† 1 x) (EReal.coe_lt_top _) with N hNN
      cases' N with N
      Â· exfalso; exact (lt_self_iff_false _).mp hNN
      Â· use N
        norm_cast at hNN
        exact lt_trans hN hNN
    Â· use N

lemma divergentSet_measurable
    {f : Î± â†’ Î±} (hf : Measurable f)
    {Ï† : Î± â†’ â„} (hÏ† : Measurable Ï†) :
    MeasurableSet (divergentSet f Ï†) :=
      measurableSet_preimage (birkhoffSup_measurable hf hÏ†) (measurableSet_singleton _)

lemma divergentSet_mem_invalg
    {f : Î± â†’ Î±} (hf : Measurable f)
    {Ï† : Î± â†’ â„} (hÏ† : Measurable Ï†) :
    MeasurableSet[invariantSets f] (divergentSet f Ï†) :=
  /- should be `Set.ext divergentSet_invariant` but it is VERY slow -/
  âŸ¨divergentSet_measurable hf hÏ†, funext (Î» _ â†¦ propext divergentSet_invariant)âŸ©

lemma birkhoffMax_tendsto_top_mem_divergentSet (hx : x âˆˆ divergentSet f Ï†) :
    Tendsto (birkhoffMax f Ï† Â· x) atTop atTop := by
  apply tendsto_atTop_atTop.mpr
  intro b
  simp only [divergentSet, Set.mem_preimage, birkhoffSup, Set.mem_singleton_iff, iSup_eq_top] at hx
  cases' hx b (EReal.coe_lt_top _) with N hN
  norm_cast at hN
  use N
  exact fun n hn â†¦ le_trans (le_of_lt hN) (le_partialSups_of_le (birkhoffSum f Ï† âˆ˜ .succ) hn x )

lemma birkhoffMaxDiff_tendsto_of_mem_divergentSet (hx : x âˆˆ divergentSet f Ï†) :
    Tendsto (birkhoffMaxDiff f Ï† Â· x) atTop (ğ“ (Ï† x)) := by
  have hx' : f x âˆˆ divergentSet f Ï† := divergentSet_invariant.mpr hx
  simp_rw [birkhoffMaxDiff_aux]
  nth_rw 2 [â† sub_zero (Ï† x)]
  apply Tendsto.sub tendsto_const_nhds
  cases' tendsto_atTop_atTop.mp (birkhoffMax_tendsto_top_mem_divergentSet hx') 0 with N hN
  exact tendsto_atTop_of_eventually_const (iâ‚€ := N) fun i hi â†¦ inf_of_le_left (hN i hi)

abbrev nonneg : Filter â„ := â¨… Îµ > 0, ğ“Ÿ (Set.Iio Îµ)

lemma birkhoffAverage_tendsto_nonpos_of_not_mem_divergentSet
    (hx : x âˆ‰ divergentSet f Ï†) :
    Tendsto (birkhoffAverage â„ f Ï† Â· x) atTop nonneg := by
  /- it suffices to show there are upper bounds â‰¤ Îµ for all Îµ > 0 -/
  simp only [tendsto_iInf, gt_iff_lt, tendsto_principal, Set.mem_Iio, eventually_atTop, ge_iff_le]
  intro Îµ hÎµ

  /- from `hx` hypothesis, the birkhoff sums are bounded above -/
  simp only [divergentSet, Set.mem_preimage, birkhoffSup, Set.mem_singleton_iff, iSup_eq_top,
    not_forall, not_exists, not_lt, exists_prop] at hx
  rcases hx with âŸ¨M', M_lt_top, M_is_boundâŸ©

  /- the upper bound is, in fact, a real number -/
  cases' M' using EReal.rec with M
  case h_bot => exfalso; exact (EReal.bot_lt_coe _).not_le (M_is_bound 0)
  case h_top => contradiction
  norm_cast at M_is_bound

  /- use archimedian property of reals -/
  cases' Archimedean.arch M hÎµ with N hN
  have upperBound (n : â„•) (hn : N â‰¤ n) : birkhoffAverage â„ f Ï† (n + 1) x < Îµ
  Â· have : M < (n + 1) â€¢ Îµ
    Â· exact hN.trans_lt $ smul_lt_smul_of_pos_right (Nat.lt_succ_of_le hn) hÎµ
    Â· rw [nsmul_eq_smul_cast â„] at this
      exact (inv_smul_lt_iff_of_pos (Nat.cast_pos.mpr (Nat.zero_lt_succ n))).mpr
        ((M_is_bound n).trans_lt this)

  /- conclusion -/
  use N + 1
  intro n hn
  specialize upperBound n.pred (Nat.le_pred_of_lt hn)
  rwa [â† Nat.succ_pred_eq_of_pos (Nat.zero_lt_of_lt hn)]

/- From now on, assume f is measure-preserving and Ï† is integrable. -/
variable {f : Î± â†’ Î±} (hf : MeasurePreserving f Î¼ Î¼)
         {Ï† : Î± â†’ â„} (hÏ† : Integrable Ï† Î¼) (hÏ†' : Measurable Ï†) /- seems necessary? -/

lemma iterates_integrable : Integrable (Ï† âˆ˜ f^[i]) Î¼ := by
  apply (integrable_map_measure _ _).mp
  Â· rwa [(hf.iterate i).map_eq]
  Â· rw [(hf.iterate i).map_eq]
    exact hÏ†.aestronglyMeasurable
  exact (hf.iterate i).measurable.aemeasurable

lemma birkhoffSum_integrable : Integrable (birkhoffSum f Ï† n) Î¼ :=
  integrable_finset_sum _ fun _ _ â†¦ iterates_integrable Î¼ hf hÏ†

lemma birkhoffMax_integrable : Integrable (birkhoffMax f Ï† n) Î¼ := by
  unfold birkhoffMax
  induction' n with n hn
  Â· simpa
  Â· rw [partialSups_succ, Function.comp_apply]
    exact Integrable.sup hn (birkhoffSum_integrable Î¼ hf hÏ†)

lemma birkhoffMaxDiff_integrable : Integrable (birkhoffMaxDiff f Ï† n) Î¼ := by
  apply Integrable.sub (birkhoffMax_integrable Î¼ hf hÏ†)
  apply (integrable_map_measure _ hf.measurable.aemeasurable).mp <;> rw [hf.map_eq]
  Â· exact birkhoffMax_integrable Î¼ hf hÏ†
  Â· exact (birkhoffMax_integrable Î¼ hf hÏ†).aestronglyMeasurable

lemma abs_le_bound {a b c : â„} : a â‰¤ b â†’ b â‰¤ c â†’ abs b â‰¤ max (abs a) (abs c) := by
  simp_rw [abs_eq_max_neg, max_le_iff]; aesop

lemma int_birkhoffMaxDiff_in_divergentSet_tendsto :
    Tendsto (Î» n â†¦ âˆ« x in divergentSet f Ï†, birkhoffMaxDiff f Ï† n x âˆ‚Î¼) atTop
            (ğ“ $ âˆ« x in divergentSet f Ï†, Ï† x âˆ‚ Î¼) := by
  apply MeasureTheory.tendsto_integral_of_dominated_convergence (abs Ï† âŠ” abs (birkhoffMaxDiff f Ï† 0))
  Â· exact fun _ â†¦ (birkhoffMaxDiff_integrable Î¼ hf hÏ†).aestronglyMeasurable.restrict
  Â· apply Integrable.sup <;> apply Integrable.abs
    Â· exact hÏ†.restrict
    Â· exact (birkhoffMaxDiff_integrable Î¼ hf hÏ†).restrict
  Â· intro n
    apply ae_of_all
    intro x
    rw [Real.norm_eq_abs]
    exact abs_le_bound (by simp [birkhoffMaxDiff_aux]) (birkhoffMaxDiff_antitone (zero_le n) _)
  Â· exact (ae_restrict_iff' (divergentSet_measurable hf.measurable hÏ†')).mpr
      (ae_of_all _ fun _ hx â†¦ birkhoffMaxDiff_tendsto_of_mem_divergentSet hx)

lemma int_birkhoffMaxDiff_in_divergentSet_nonneg :
    0 â‰¤ âˆ« x in divergentSet f Ï†, birkhoffMaxDiff f Ï† n x âˆ‚Î¼ := by
  unfold birkhoffMaxDiff
  have : (Î¼.restrict (divergentSet f Ï†)).map f = Î¼.restrict (divergentSet f Ï†)
  Â· nth_rw 1 [
      â† (divergentSet_mem_invalg hf.measurable hÏ†').2,
      â† Î¼.restrict_map hf.measurable (divergentSet_measurable hf.measurable hÏ†'),
      hf.map_eq
    ]
  have mi {n : â„•} := birkhoffMax_integrable Î¼ hf hÏ† (n := n)
  have mm {n : â„•} := birkhoffMax_measurable hf.measurable hÏ†' (n := n)
  rw [integral_sub, sub_nonneg]
  Â· rw [â† integral_map (hf.aemeasurable.restrict) mm.aestronglyMeasurable, this]
    exact integral_mono mi.restrict mi.restrict ((birkhoffMax f Ï†).monotone (Nat.le_succ _))
  Â· exact mi.restrict
  Â· apply (integrable_map_measure mm.aestronglyMeasurable hf.aemeasurable.restrict).mp
    rw [this]
    exact mi.restrict

lemma int_in_divergentSet_nonneg : 0 â‰¤ âˆ« x in divergentSet f Ï†, Ï† x âˆ‚Î¼ :=
  le_of_tendsto_of_tendsto' tendsto_const_nhds
    (int_birkhoffMaxDiff_in_divergentSet_tendsto Î¼ hf hÏ† hÏ†')
    (Î» _ â†¦ int_birkhoffMaxDiff_in_divergentSet_nonneg Î¼ hf hÏ† hÏ†')

/- these seem to be missing? -/
lemma nullMeasurableSpace_le [ms : MeasurableSpace Î±] {Î¼ : Measure Î±} :
    ms â‰¤ NullMeasurableSpace.instMeasurableSpace (Î± := Î±) (Î¼ := Î¼) :=
  Î» s hs â†¦ âŸ¨s, hs, ae_eq_refl sâŸ©

lemma divergentSet_zero_meas_of_condexp_neg
    (h : âˆ€áµ x âˆ‚Î¼, (Î¼[Ï†|invariantSets f]) x < 0) :
    Î¼ (divergentSet f Ï†) = 0 := by
  have pos : âˆ€áµ x âˆ‚Î¼.restrict (divergentSet f Ï†), 0 < -(Î¼[Ï†|invariantSets f]) x
  Â· exact ae_restrict_of_ae (h.mono Î» _ hx â†¦ neg_pos.mpr hx)
  have ds_meas := divergentSet_mem_invalg hf.measurable hÏ†'
  by_contra hm; simp_rw [â† pos_iff_ne_zero] at hm
  have : âˆ« x in divergentSet f Ï†, Ï† x âˆ‚Î¼ < 0
  Â· rw [â† set_integral_condexp invariantSets_le hÏ† ds_meas,
      â† Left.neg_pos_iff, â† integral_neg, integral_pos_iff_support_of_nonneg_ae]
    Â· unfold Function.support
      rw [(ae_iff_measure_eq _).mp]
      Â· rwa [Measure.restrict_apply_univ _]
      Â· conv in _ â‰  _ => rw [ne_comm]
        exact Eventually.ne_of_lt pos
      Â· apply measurableSet_support _
        apply (stronglyMeasurable_condexp).measurable.neg.le _
        exact (le_trans invariantSets_le nullMeasurableSpace_le)
    Â· exact ae_le_of_ae_lt pos
    Â· exact integrable_condexp.restrict.neg
  exact this.not_le (int_in_divergentSet_nonneg Î¼ hf hÏ† hÏ†')

lemma limsup_birkhoffAverage_nonpos_of_condexp_neg
    (h : âˆ€áµ x âˆ‚Î¼, (Î¼[Ï†|invariantSets f]) x < 0) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (birkhoffAverage â„ f Ï† Â· x) atTop nonneg := by
  apply Eventually.mono _ Î» _ â†¦ birkhoffAverage_tendsto_nonpos_of_not_mem_divergentSet
  apply ae_iff.mpr; simp
  exact divergentSet_zero_meas_of_condexp_neg Î¼ hf hÏ† hÏ†' h

def invCondexp (Î¼ : Measure Î± := by volume_tac) [IsProbabilityMeasure Î¼]
    (f : Î± â†’ Î±) (Ï† : Î± â†’ â„) : Î± â†’ â„ := Î¼[Ï†|invariantSets f]

theorem birkhoffErgodicTheorem_aux (Îµ : â„) (hÎµ : 0 < Îµ) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (birkhoffAverage â„ f Ï† Â· x - (invCondexp Î¼ f Ï† x + Îµ)) atTop nonneg := by
  let Ïˆ := Ï† - (invCondexp Î¼ f Ï† + Î» _ â†¦ Îµ)
  have Ïˆ_integrable : Integrable Ïˆ Î¼ := hÏ†.sub (integrable_condexp.add (integrable_const _))
  have Ïˆ_measurable : Measurable Ïˆ
  Â· suffices Measurable (invCondexp Î¼ f Ï†) by measurability
    exact stronglyMeasurable_condexp.measurable.le (invariantSets_le)

  let condexpÏˆ := invCondexp Î¼ f Ïˆ
  have condexpÏˆ_const : condexpÏˆ =áµ[Î¼] - Î» _ â†¦ Îµ := calc
    Î¼[Ïˆ|invariantSets f]
    _ =áµ[Î¼] _ - _ := condexp_sub hÏ† (integrable_condexp.add (integrable_const _))
    _ =áµ[Î¼] _ - (_ + _) := (condexp_add integrable_condexp (integrable_const _)).neg.add_left
    _ =áµ[Î¼] _ - (_ + _) := (condexp_condexp_of_le (le_of_eq rfl)
                              invariantSets_le).add_right.neg.add_left
    _ = - Î¼[Î» _ â†¦ Îµ|invariantSets f] := by simp
    _ = - Î» _ â†¦ Îµ := by rw [condexp_const invariantSets_le]

  have limsup_nonpos : âˆ€áµ x âˆ‚Î¼, Tendsto (birkhoffAverage â„ f Ïˆ Â· x) atTop nonneg
  Â· suffices âˆ€áµ x âˆ‚Î¼, condexpÏˆ x < 0 from
      limsup_birkhoffAverage_nonpos_of_condexp_neg Î¼ hf Ïˆ_integrable Ïˆ_measurable this
    exact condexpÏˆ_const.mono Î» x hx â†¦ by simp [hx, hÎµ]

  refine limsup_nonpos.mono Î» x hx => ?_

  suffices âˆ€ (n : â„•), 0 < n â†’ birkhoffAverage â„ f Ïˆ n x = birkhoffAverage â„ f Ï† n x - (invCondexp Î¼ f Ï† x + Îµ) by
    simp at hx âŠ¢
    intro r hr
    cases' hx r hr with n hn
    use n + 1
    intro k hk
    rw [â† this k (Nat.zero_lt_of_lt hk)]
    exact hn k (Nat.le_of_succ_le hk)

  have condexpÏ†_invariant : invCondexp Î¼ f Ï† âˆ˜ f = invCondexp Î¼ f Ï† :=
    InvariantSets.invariant_of_measurable _ _ stronglyMeasurable_condexp.measurable

  intro n hn
  simp [Ïˆ, birkhoffAverage_sub, birkhoffAverage_add, birkhoffAverage_eq_of_invariant
    (show _ = Î» _ â†¦ Îµ from rfl) hn, birkhoffAverage_eq_of_invariant condexpÏ†_invariant hn]

theorem birkhoffErgodicTheorem :
    âˆ€áµ x âˆ‚Î¼, Tendsto (birkhoffAverage â„ f Ï† Â· x) atTop (ğ“ (invCondexp Î¼ f Ï† x)) := by
  have : âˆ€áµ x âˆ‚Î¼, âˆ€ (k : {k : â„• // k > 0}),
    âˆ€á¶  n in atTop,
      |birkhoffAverage â„ f Ï† n x - (invCondexp Î¼ f Ï† x)| < (k : â„)â»Â¹
  Â· apply ae_all_iff.mpr
    rintro âŸ¨k, hkâŸ©
    let Î´ := (k : â„)â»Â¹/2
    have hÎ´ : Î´ > 0 := by simpa [Î´]
    have pâ‚ := birkhoffErgodicTheorem_aux Î¼ hf hÏ† hÏ†' Î´ hÎ´
    have pâ‚‚ := birkhoffErgodicTheorem_aux Î¼ hf hÏ†.neg hÏ†'.neg Î´ hÎ´
    have : invCondexp Î¼ f (-Ï†) =áµ[Î¼] -invCondexp Î¼ f Ï† := condexp_neg _
    refine ((pâ‚.and pâ‚‚).and this).mono Î» x âŸ¨âŸ¨hxâ‚, hxâ‚‚âŸ©, hxâ‚ƒâŸ© => ?_
    simp at hxâ‚ hxâ‚‚ âŠ¢
    cases' hxâ‚ Î´ hÎ´ with nâ‚ hnâ‚
    cases' hxâ‚‚ Î´ hÎ´ with nâ‚‚ hnâ‚‚
    simp_rw [Î´] at hnâ‚ hnâ‚‚ âŠ¢
    use (max nâ‚ nâ‚‚)
    intro m hm
    apply abs_lt.mpr
    constructor
    Â· specialize hnâ‚‚ m (le_of_max_le_right hm)
      rw [hxâ‚ƒ, birkhoffAverage_neg] at hnâ‚‚
      norm_num at hnâ‚‚
      linarith
    Â· specialize hnâ‚ m (le_of_max_le_left hm)
      linarith

  refine this.mono Î» x hx => Metric.tendsto_atTop.mpr Î» Îµ hÎµ => ?_
  cases' Archimedean.arch 1 hÎµ with k hk
  have hk' : 1 < (k + 1) â€¢ Îµ
  Â· exact hk.trans_lt $ smul_lt_smul_of_pos_right (lt_add_one k) hÎµ
  simp only [eventually_atTop, ge_iff_le, Subtype.forall, gt_iff_lt] at hx
  cases' hx k.succ (Nat.zero_lt_succ k) with N hN
  use N
  intro n hn
  apply (hN n hn).trans
  rw [inv_pos_lt_iff_one_lt_mul (Nat.cast_pos.mpr k.succ_pos)]
  norm_num at hk' âŠ¢
  linarith

#print axioms birkhoffErgodicTheorem
